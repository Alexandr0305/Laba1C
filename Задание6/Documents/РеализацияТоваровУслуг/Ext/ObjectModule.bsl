

&После("ОбработкаПроведения")
Процедура ШСВ_ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ШСВ_Методика = "Старая" Тогда
		
		ОбщегоНазначения.СообщитьПользователю("Документ проведён по старой методике");
		Движения.ШСВ_ОстаткиНоменклатуры.Очистить();
		Движения.ШСВ_ОстаткиНоменклатуры.Записывать = Истина;
		Движения.Записать();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РеализацияТоваровУслугТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|ГДЕ
		|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровУслугТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Количество КАК Количество,
		|	ЕСТЬNULL(ШСВ_ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
		|ИЗ
		|	Товары КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ШСВ_ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						Товары.Номенклатура
		|					ИЗ
		|						Товары КАК Товары)) КАК ШСВ_ОстаткиНоменклатурыОстатки
		|		ПО Товары.Номенклатура = ШСВ_ОстаткиНоменклатурыОстатки.Номенклатура";
		
		Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить(); 
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ШСВ_ОстаткиНоменклатуры");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
		Блокировка.Заблокировать();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ОстатокНаСкладе =  ВыборкаДетальныеЗаписи.КоличествоОстаток - ВыборкаДетальныеЗаписи.Количество;
			Если ОстатокНаСкладе < 0 Тогда
				Отказ = Истина;
				ТекстПользователю = СтрШаблон("Не хватает %1 в количестве %2", ВыборкаДетальныеЗаписи.Номенклатура, ОстатокНаСкладе);
				ОбщегоНазначения.СообщитьПользователю(ТекстПользователю);
			Иначе	
				Движение = Движения.ШСВ_ОстаткиНоменклатуры.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				Движение.Склад = Склад;
				Движение.Количество = ВыборкаДетальныеЗаписи.Количество;
				Движение.Организация = Организация;
			КонецЕсли;
			
			
		КонецЦикла;
		Движения.ШСВ_ОстаткиНоменклатуры.Записывать = Истина;
		
	ИначеЕсли ШСВ_Методика = "Новая" Тогда 
		ОбщегоНазначения.СообщитьПользователю("Документ проведён по новой методике");
	
		МенеджерВТ = Новый МенеджерВременныхТаблиц;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РеализацияТоваровУслугТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|ГДЕ
		|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровУслугТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Количество КАК Количество
		|ИЗ
		|	Товары КАК Товары";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Движения.ШСВ_ОстаткиНоменклатуры.Очистить();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Движение = Движения.ШСВ_ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Количество = ВыборкаДетальныеЗаписи.Количество;
			Движение.Склад = Склад;
			Движение.Организация = Организация;
			
		КонецЦикла; 
		
		Движения.ШСВ_ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
		
		Движения.ШСВ_ОстаткиНоменклатуры.Записывать = Истина;
		Движения.Записать(); 
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваровОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ШСВ_ОстаткиНоменклатуры.Остатки(
		|			&МоментВремени,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					Товары.Номенклатура КАК Номенклатура
		|				ИЗ
		|					Товары КАК Товары)) КАК ОстаткиТоваровОстатки
		|ГДЕ
		|	ОстаткиТоваровОстатки.КоличествоОстаток < 0";
		
		ГраницаКонтроля = Новый Граница(МоментВремени(), ВидГраницы.Включая);
		Запрос.УстановитьПараметр("МоментВремени", ГраницаКонтроля);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Отказ = Истина;
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				ТекстПользователю = СтрШаблон("Не хватает %1 в количестве %2", ВыборкаДетальныеЗаписи.Номенклатура, ВыборкаДетальныеЗаписи.КоличествоОстаток);
				ОбщегоНазначения.СообщитьПользователю(ТекстПользователю);
			КонецЦикла;
			
			
		КонецЕсли;
		
		Если Отказ Тогда
			
			Возврат;
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры
