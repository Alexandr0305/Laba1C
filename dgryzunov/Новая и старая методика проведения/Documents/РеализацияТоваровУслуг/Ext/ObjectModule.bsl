
&После("ОбработкаПроведения")
Процедура ОР_ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПровестиПоСтаройМетодике = ЭтотОбъект.ОР_Методика;
	
	Если ПровестиПоСтаройМетодике Тогда
		
		ПроведениеПоСтаройМетодике(Отказ, РежимПроведения)	
	Иначе
		ПроведениеПоНовойМетодике(Отказ, РежимПроведения);
	КонецЕсли;
КонецПроцедуры 

Процедура ПроведениеПоНовойМетодике(Отказ, РежимПроведения)
	Движения.ОР_ОстаткиНоменклатуры.Записывать = Истина;
	
	Запрос = Новый Запрос;
	МВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслуг.Склад КАК Склад,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	СУММА(РеализацияТоваровУслугТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_ТоварыИзДокумента
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары,
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	И РеализацияТоваровУслугТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугТовары.Номенклатура,
	|	РеализацияТоваровУслуг.Склад,
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТоварыИзДокумента.Дата КАК Период,
	|	ВТ_ТоварыИзДокумента.Номенклатура КАК Номенклатура,
	|	ВТ_ТоварыИзДокумента.Склад КАК Склад,
	|	ВТ_ТоварыИзДокумента.Организация КАК Организация,
	|	ВТ_ТоварыИзДокумента.Количество КАК Количество
	|ИЗ
	|	ВТ_ТоварыИзДокумента КАК ВТ_ТоварыИзДокумента "; 
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ОР_ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ВТ_ТоварыИзДокумента.Номенклатура) КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(ВТ_ТоварыИзДокумента.Склад) КАК Склад,
	|	ЕСТЬNULL(ОР_ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК Остаток
	|ИЗ
	|	ВТ_ТоварыИзДокумента КАК ВТ_ТоварыИзДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОР_ОстаткиНоменклатуры.Остатки(
	|				&Период,
	|				(Номенклатура, Склад) В
	|					(ВЫБРАТЬ
	|						ВТ_ТоварыИзДокумента.Номенклатура,
	|						ВТ_ТоварыИзДокумента.Склад
	|					ИЗ
	|						ВТ_ТоварыИзДокумента КАК ВТ_ТоварыИзДокумента)) КАК ОР_ОстаткиНоменклатурыОстатки
	|		ПО ВТ_ТоварыИзДокумента.Склад = ОР_ОстаткиНоменклатурыОстатки.Склад
	|			И ВТ_ТоварыИзДокумента.Номенклатура = ОР_ОстаткиНоменклатурыОстатки.Номенклатура
	|ГДЕ
	|	ЕСТЬNULL(ОР_ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) < 0"; 
	
	Запрос.УстановитьПараметр("Период", ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, Новый Граница(МоментВремени(), ВидГраницы.Включая)));
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СообщениеПользователю = СтрШаблон(НСтр("ru = 'Не хватает товара %1 по складу %2 в количестве %3 шт.'"), Выборка.Номенклатура, Выборка.Склад, Выборка.Остаток);
		ОбщегоНазначения.СообщитьПользователю(СообщениеПользователю);
		Отказ = Истина;
	КонецЦикла; 
КонецПроцедуры

Процедура ПроведениеПоСтаройМетодике(Отказ, РежимПроведения) 
	
	Движения.ОР_ОстаткиНоменклатуры.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Дата КАК Период,
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслуг.Склад КАК Склад,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	СУММА(РеализацияТоваровУслугТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_ТоварыИзДокумента
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары,
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	И РеализацияТоваровУслугТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслуг.Склад,
	|	РеализацияТоваровУслугТовары.Номенклатура,
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТоварыИзДокумента.Период КАК Период,
	|	ВТ_ТоварыИзДокумента.Номенклатура КАК Номенклатура,
	|	ВТ_ТоварыИзДокумента.Склад КАК Склад,
	|	ВТ_ТоварыИзДокумента.Организация КАК Организация,
	|	СУММА(ЕСТЬNULL(ВТ_ТоварыИзДокумента.Количество, 0)) КАК Количество,
	|	СУММА(ЕСТЬNULL(ОР_ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0)) КАК КоличествоОстаток
	|ИЗ
	|	ВТ_ТоварыИзДокумента КАК ВТ_ТоварыИзДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОР_ОстаткиНоменклатуры.Остатки(
	|				&МоментВремени,
	|				(Номенклатура, Склад) В
	|					(ВЫБРАТЬ
	|						ВТ_ТоварыИзДокумента.Номенклатура КАК Номенклатура,
	|						ВТ_ТоварыИзДокумента.Склад КАК Склад
	|					ИЗ
	|						ВТ_ТоварыИзДокумента КАК ВТ_ТоварыИзДокумента)) КАК ОР_ОстаткиНоменклатурыОстатки
	|		ПО ВТ_ТоварыИзДокумента.Номенклатура = ОР_ОстаткиНоменклатурыОстатки.Номенклатура
	|			И ВТ_ТоварыИзДокумента.Склад = ОР_ОстаткиНоменклатурыОстатки.Склад
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТоварыИзДокумента.Номенклатура,
	|	ВТ_ТоварыИзДокумента.Склад,
	|	ВТ_ТоварыИзДокумента.Период,
	|	ВТ_ТоварыИзДокумента.Организация";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка); 
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		
		НеХватает = ВыборкаДетальныеЗаписи.Количество - ВыборкаДетальныеЗаписи.КоличествоОстаток;
		Если НеХватает > 0 Тогда 
			
			СообщениеПользователю = СтрШаблон(НСтр("ru = 'Не хватает товара %1 по складу %2 в количестве %3 шт.'"), ВыборкаДетальныеЗаписи.Номенклатура, ВыборкаДетальныеЗаписи.Склад, НеХватает);
			ОбщегоНазначения.СообщитьПользователю(СообщениеПользователю);
			Отказ = Истина;
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		Движение = Движения.ОР_ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
	КонецЦикла;
КонецПроцедуры
